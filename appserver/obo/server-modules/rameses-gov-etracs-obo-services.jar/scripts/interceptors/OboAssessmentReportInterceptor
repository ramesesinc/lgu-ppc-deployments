import com.rameses.annotations.*; 

class OboAssessmentReportInterceptor {
	
	@Service("DateService")
	def dateSvc;

	@DataContext("obo_app_fee")
	def feeEm;

	@DataContext("vw_obo_app")
	def vwAppEm;

	@After(pattern="FormReportService.getData", eval="#{ args[0].reportid == 'obo_assessment' }")
	public void getOboAssessmentReport( def evt ) {
		def p = evt.args[0];
		def result = evt.result;
		def reportid = p.reportid;
		def objid = p.parameters.objid;
		if(!objid) throw new Exception("objid is required in assessment report")

		def data = vwAppEm.find( [objid: objid ]).first();
		if(!data) throw new Exception("App not found");

		data.permittype =  data.doctitle ;

		//group by fee group
		def vitems = [];
		def fitems = feeEm.find([appid: objid]).list();
		fitems.groupBy{ it.item.item }.each {k,v->
			vitems << [ item: k, 
				feegroup: v.max{ it.item.feegroup }.item.feegroup,
				sortorder: v.max{ it.item.sortorder }.item.sortorder, 
				amount: v.sum{it.amount} 
			];
		};
		data.items = vitems.collect {
			[ item: it.item, section: it.feegroup?.title, amount: it.amount, 
				index1: it.feegroup.sortindex,
				index2: it.sortorder 
			]
		}.sort{ [it.index1, it.index2] }
		data.dtfiled = dateSvc.getServerDate();
		result.data = data;
	}	

	@Before(pattern="JasperEmailQueueService.send", eval="#{ args[0].reportid == 'obo_assessment' }")
	public void beforeSendEmail( def evt ) {
		def p = evt.args[0];
		def reportid = p.reportid;
		def ea = vwAppEm.find( [objid: p.refid ] ).first();
		if ( !p.to ) p.to = ea.contact.email;
		p.info = ea;
	}

	@Before(pattern="JasperReportEmailService.send", eval="#{ args[0].reportid == 'obo_assessment' }", index=-10000)
    public void emailAssessment(def evt) {
        def o = evt.args[0];
        def refid = o.refid;
        o.attachments = [
            [ title: "OrderOfPayment.pdf", reportid: o.reportid, parameters: [objid: refid ], 
            	template: "reports/obo/assessment" ]
        ];
        o.to = o.to;
        o.subject = o.subject;
        o.message = o.message;
        o.connection = "obo-email-server";
    }   


}